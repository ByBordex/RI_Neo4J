= THE WITCHER: NEO4J
==DOMINIO DE LA APLICACIÓN
El dominio que hemos elegido está basado en el mundo de una saga de libros  "La saga de Geralt de Rivia", en la que se basan los videojuegos "The Witcher". Este mundo abarca personajes, que nacen en ciudades, que a su vez pueden pertenecer a países y estos a facciones. Estas facciones pueden tomar parte en armisticios, que ocurren en países. Algunos personajes pueden haber sido adiestrados en una escuela de brujo (situadas en ciudades) , que es una profesión que se encarga de eliminar a las bestias. Las bestias habitan en diferentes ciudades. 
A continuación se muestra un gráfico que explica las relaciones que hemos establecido en este grafo. 
image:images/esquema.png[Esquema, title="Esquema"]

==REPRESENTACIÓN GRÁFICA DE LA INSTANCIA
La representación gráfica de una instancia de la base de datos, la hemos hecho cogiendo unos cuantos nodos de cada categoría, y haciendo una simple representación mediante neo4j: 
image:images/graph.png[Grafo, title="Grafo"]

==SCRIPT DE LA BASE DE DATOS
//hide
//setup
[source,cypher]
----
//Creacion nodos de personajes.
CREATE (Geralt:Personaje {nombre_personaje:'Geralt'})
CREATE (Lambert:Personaje {nombre_personaje:'Lambert'})
CREATE (Vesemir:Personaje {nombre_personaje:'Vesemir'})
CREATE (Eskel:Personaje {nombre_personaje:'Eskel'})
CREATE (Jaskier:Personaje {nombre_personaje:'Jaskier'})
CREATE (Emhyr:Personaje {nombre_personaje:'Emhyr var Emreis'})
CREATE (Yennefer:Personaje {nombre_personaje:'Yennefer'})
CREATE (Letho:Personaje {nombre_personaje:'Letho'})
CREATE (Foltest:Personaje {nombre_personaje:'Foltest'})
CREATE (JadKaradin:Personaje {nombre_personaje:'Jad Karadin'})
CREATE (Eredin:Personaje {nombre_personaje:'Eredin'})
CREATE (Toruviel:Personaje {nombre_personaje:'Toruviel'})
CREATE (Avallach:Personaje {nombre_personaje:"Avallac'h"})
CREATE (Jerome:Personaje {nombre_personaje:'Jerome Moreau'})
CREATE (Zoltan:Personaje {nombre_personaje:'Zoltan'})
CREATE (Triss:Personaje {nombre_personaje:'Triss'})
CREATE (Ciri:Personaje {nombre_personaje:'Ciri'})
CREATE (Bran:Personaje {nombre_personaje:'Rey Bran'})

//Creacíon nodos de bestiario.
CREATE (Arpia:Bestia {nombre_bestia:'Arpia'})
CREATE (Moira:Bestia {nombre_bestia:'Moira'})
CREATE (Doppler:Bestia {nombre_bestia:'Doppler'})
CREATE (Ghul:Bestia {nombre_bestia:'Ghul'})
CREATE (Nekker:Bestia {nombre_bestia:'Nekker'})
CREATE (Malogrado:Bestia {nombre_bestia:'Malogrado'})
CREATE (Sirena:Bestia {nombre_bestia:'Sirena'})
CREATE (Aparicion:Bestia {nombre_bestia:'Aparicion'})
CREATE (Dama:Bestia {nombre_bestia:'Dama del mediodia'})
CREATE (Basilisco:Bestia {nombre_bestia:'Basilisco'})
CREATE (Djinn:Bestia {nombre_bestia:'Djinn'})
CREATE (Pantera:Bestia {nombre_bestia:'Pantera'})

//Creacion nodos ciudades.
CREATE (Tir:Ciudad {name_ciudad:'Tir na Lia'})
CREATE (CintraC:Ciudad {name_ciudad:'Cintra'})
CREATE (Guleta:Ciudad {name_ciudad:'Guleta'})
CREATE (Novigrado:Ciudad {name_ciudad:'Novigrado'})
CREATE (Vergenberg:Ciudad {name_ciudad:'Vergenberg'})
CREATE (KaerMorhenC:Ciudad {name_ciudad:'Kaer Morhen'})
CREATE (TemeriaC:Ciudad {name_ciudad:'Temeria'})
CREATE (Dol:Ciudad {name_ciudad:'Dol Blathanna'})
CREATE (Maribor:Ciudad {name_ciudad:'Maribor'})
CREATE (NilfgaardC:Ciudad {name_ciudad:'Nilfgaard'})
CREATE (RiviaC:Ciudad {name_ciudad:'Rivia'})
CREATE (Tretogor:Ciudad {name_ciudad:'Tretogor'})
CREATE (Touissant:Ciudad {name_ciudad:'Touissant'})
CREATE (OxenfurtC:Ciudad {name_ciudad: 'Oxenfurt'})
CREATE (NovigradoC:Ciudad {name_ciudad: 'Novigrado'})
CREATE (Wyzima:Ciudad {name_ciudad: 'Wyzima'})
CREATE (ArdCarraigh:Ciudad {name_ciudad: 'ArdCarraigh'})
CREATE (KaerTrolde:Ciudad {name_ciudad: 'Kaer Trolde'})


//Creacion nodos paises
CREATE (NilfgaardP:Pais {nombre_pais:'Nilfgaard'})
CREATE (RiviaP:Pais {nombre_pais:'Rivia'})
CREATE (Redania:Pais {nombre_pais:'Redania'})
CREATE (Temeria:Pais {nombre_pais:'Temeria'})
CREATE (Kaedwen:Pais {nombre_pais:'Kaedwen'})
CREATE (Kovir:Pais {nombre_pais:'Kovir'})
CREATE (Aedirn:Pais {nombre_pais:'Aedirn'})
CREATE (CintraP:Pais {nombre_pais:'Cintra'})
CREATE (Sodden:Pais {nombre_pais:'Sodden'})
CREATE (ArdSkellig:Pais {nombre_pais:'Ard Skellig'})
CREATE (Undvik:Pais {nombre_pais:'Undvik'})
CREATE (Faroe:Pais {nombre_pais:'Faroe'})
CREATE (Thanedd:Pais {nombre_pais:'Thanedd'})

//Creacion nodos facciones
CREATE (NilfgaardF:Faccion {nombre_faccion:'Nilfgaard'})
CREATE (Brujos:Faccion {nombre_faccion:'Brujos'})
CREATE (Logia:Faccion {nombre_faccion:'La logia'})
CREATE (Scoiatael:Faccion {nombre_faccion:'Scoia’tael'})
CREATE (ReinosDelNorte:Faccion {nombre_faccion:'Reinos del norte'})
CREATE (CaceriaSalvaje:Faccion {nombre_faccion:'Caceria salvaje'})
CREATE (Skellige:Faccion {nombre_faccion:'Islas Skellige'})

//Creacion nodos escuelaBrujos
CREATE (Lobo:EscuelaBrujos {nombre_escuela:'Lobo'})
CREATE (Vibora:EscuelaBrujos {nombre_escuela:'Vibora'})
CREATE (Grifo:EscuelaBrujos {nombre_escuela:'Grifo'})
CREATE (Gato:EscuelaBrujos {nombre_escuela:'Gato'})

//Creacion nodos armisticio
CREATE (BatallaSodden:Armisticio {nombre_armisticio:'Batalla del Monte Sodden'})
CREATE (GolpeEstadoThanedd:Armisticio {nombre_armisticio:'Golpe de Estado de Thanedd'})


//RELACIONES

//Relaciones BORN
CREATE
  (Geralt)-[:BORN]->(RiviaC),
  (Lambert)-[:BORN]->(KaerMorhenC),
  (Vesemir)-[:BORN]->(KaerMorhenC),
  (Emhyr)-[:BORN]->(NilfgaardC),
  (Jaskier)-[:BORN]->(OxenfurtC),
  (Yennefer)-[:BORN]->(Vergenberg),
  (Letho)-[:BORN]->(Guleta),
  (Foltest)-[:BORN]->(TemeriaC),
  (JadKaradin)-[:BORN]->(NovigradoC),
  (Eredin)-[:BORN]->(Tir),
  (Toruviel)-[:BORN]->(Dol),
  (Avallach)-[:BORN]->(Tir),
  (Jerome)-[:BORN]->(Touissant),
  (Zoltan)-[:BORN]->(NovigradoC),
  (Triss)-[:BORN]->(Maribor),
  (Ciri)-[:BORN]->(CintraC),
  (Eskel)-[:BORN]->(KaerMorhenC),
  (Bran)-[:BORN]->(KaerTrolde)

//Relaciones CAPITAL_DE
CREATE
	(NilfgaardC)-[:CAPITAL_DE]->(NilfgaardP),
	(RiviaC)-[:CAPITAL_DE]->(RiviaP),
	(Tretogor)-[:CAPITAL_DE]->(Redania),
	(Wyzima)-[:CAPITAL_DE]->(Temeria),
	(ArdCarraigh)-[:CAPITAL_DE]->(Kaedwen),
	(Vergenberg)-[:CAPITAL_DE]->(Aedirn),
	(KaerTrolde)-[:CAPITAL_DE]->(ArdSkellig),
	(CintraP)-[:CAPITAL_DE]->(CintraC)


//Relaciones PERTENECE
CREATE
	(Emhyr)-[:PERTENECE]->(NilfgaardF),
	(Geralt)-[:PERTENECE]->(Brujos),
	(Yennefer)-[:PERTENECE]->(Logia),
	(Lambert)-[:PERTENECE]->(Brujos),
	(Letho)-[:PERTENECE]->(Brujos),
	(Vesemir)-[:PERTENECE]->(Brujos),
	(Eskel)-[:PERTENECE]->(Brujos),
	(Foltest)-[:PERTENECE]->(ReinosDelNorte),
	(JadKaradin)-[:PERTENECE]->(Brujos),
	(Eredin)-[:PERTENECE]->(CaceriaSalvaje),
	(Toruviel)-[:PERTENECE]->(Scoiatael),
	(Jerome)-[:PERTENECE]->(Brujos),
	(Bran)-[:PERTENECE]->(Skellige),
	(Triss)-[:PERTENECE]->(Logia)


//Relaciones HABITA_EN
CREATE
  (Arpia)-[:HABITA_EN]->(CintraC),
  (Arpia)-[:HABITA_EN]->(KaerMorhenC),
  (Moira)-[:HABITA_EN]->(Touissant),
  (Moira)-[:HABITA_EN]->(NilfgaardC),
  (Moira)-[:HABITA_EN]->(Maribor),
  (Doppler)-[:HABITA_EN]->(Novigrado),
  (Ghul)-[:HABITA_EN]->(CintraC),
  (Ghul)-[:HABITA_EN]->(OxenfurtC),
  (Nekker)-[:HABITA_EN]->(Tir),
  (Nekker)-[:HABITA_EN]->(TemeriaC),
  (Malogrado)-[:HABITA_EN]->(Novigrado),
  (Malogrado)-[:HABITA_EN]->(Tir),
  (Malogrado)-[:HABITA_EN]->(OxenfurtC),
  (Sirena)-[:HABITA_EN]->(Touissant),
  (Sirena)-[:HABITA_EN]->(NilfgaardC),
  (Sirena)-[:HABITA_EN]->(KaerTrolde),
  (Aparicion)-[:HABITA_EN]->(KaerMorhenC),
  (Aparicion)-[:HABITA_EN]->(Tir),
  (Aparicion)-[:HABITA_EN]->(Dol),
  (Dama)-[:HABITA_EN]->(Guleta),
  (Dama)-[:HABITA_EN]->(Maribor),
  (Dama)-[:HABITA_EN]->(OxenfurtC),
  (Basilisco)-[:HABITA_EN]->(Rivia),
  (Basilisco)-[:HABITA_EN]->(TemeriaC),
  (Basilisco)-[:HABITA_EN]->(Touissant),
  (Djinn)-[:HABITA_EN]->(Guleta),
  (Djinn)-[:HABITA_EN]->(Nilfgaard),
  (Pantera)-[:HABITA_EN]->(Touissant),
  (Pantera)-[:HABITA_EN]->(Nilfgaard)

//Relacion ADRIESTRADO_EN
CREATE 
  (Geralt)-[:ADIESTRADO_EN]->(Lobo),
  (Lambert)-[:ADIESTRADO_EN]->(Lobo),
  (Vesemir)-[:ADIESTRADO_EN]->(Lobo),
  (Letho)-[:ADIESTRADO_EN]->(Vibora),
  (JadKaradin)-[:ADIESTRADO_EN]->(Gato),
  (Jerome)-[:ADIESTRADO_EN]->(Grifo),
  (Eskel)-[:ADIESTRADO_EN]->(Lobo)

//Relaciones GOBIERNA
CREATE 
	(Foltest)-[:GOBIERNA]->(Temeria),
	(Emhyr)-[:GOBIERNA]->(NilfgaardP)

//Relaciones FORMA
CREATE
	(Temeria)-[:FORMA]->(ReinosDelNorte),
	(Redania)-[:FORMA]->(ReinosDelNorte),
	(RiviaP)-[:FORMA]->(ReinosDelNorte),
	(Kaedwen)-[:FORMA]->(ReinosDelNorte),
	(Kovir)-[:FORMA]->(ReinosDelNorte),
	(Aedirn)-[:FORMA]->(ReinosDelNorte),
	(CintraP)-[:FORMA]->(ReinosDelNorte),
	(ArdSkellig)-[:FORMA]->(Skellige),
	(Undvik)-[:FORMA]->(Skellige),
	(Faroe)-[:FORMA]->(Skellige),
	(NilfgaardP)-[:FORMA]->(NilfgaardF),
	(Sodden)-[:FORMA]->(NilfgaardF)

//Relaciones TOMA_LUGAR_EN
CREATE
	(BatallaSodden)-[:TOMA_LUGAR_EN]->(Sodden),
	(GolpeEstadoThanedd)-[:TOMA_LUGAR_EN]->(Thanedd)

//Relaciones TOMA_PARTE
CREATE
	(NilfgaardF)-[:TOMA_PARTE{postura:'Defensor'}]->(GolpeEstadoThanedd),
	(Logia)-[:TOMA_PARTE{postura:'Atacante'}]->(GolpeEstadoThanedd),
	(Scoiatael)-[:TOMA_PARTE{postura:'Defensor'}]->(GolpeEstadoThanedd),
	(ReinosDelNorte)-[:TOMA_PARTE{postura:'Atacante'}]->(GolpeEstadoThanedd),
	(NilfgaardF)-[:TOMA_PARTE{postura: 'Atacante'}]->(BatallaSodden),
	(Skellige)-[:TOMA_PARTE{postura:'Defensor'}]->(BatallaSodden),
	(ReinosDelNorte)-[:TOMA_PARTE{postura:'Atacante'}]->(BatallaSodden)


//Relaciones PARTICIPA
CREATE
	(Triss)-[:PARTICIPA]->(GolpeEstadoThanedd),
	(Yennefer)-[:PARTICIPA]->(GolpeEstadoThanedd),
	(Geralt)-[:PARTICIPA]->(GolpeEstadoThanedd),

	(Triss)-[:PARTICIPA]->(BatallaSodden),
	(Yennefer)-[:PARTICIPA]->(BatallaSodden),
	(Emhyr)-[:PARTICIPA]->(BatallaSodden),
	(Foltest)-[:PARTICIPA]->(BatallaSodden)

//Relacionse SE_SITUAN
CREATE
	(Lobo)-[:SE_SITUA]->(KaerMorhenC),
	(Vibora)-[:SE_SITUA]->(NilfgaardC)

----


==CONSULTAS

===Consultas simples
*Consulta*:
//output
[source,cypher]
----
MATCH (m:EscuelaBrujos {nombre_escuela: 'Lobo'})<-[:ADIESTRADO_EN]-(a:Personaje) 
RETURN a.nombre_personaje AS Nombre 
---- 
*Objetivo*: Todos los personajes que hayan sido adiestrados en la Escuela de brujos de nombre Lobo. 
*Resultado*: 
image:images/q1.png[q1, title="q1"]

*Consulta*: 
//output
[source,cypher]
----
MATCH (m:Ciudad {name_ciudad: 'Oxenfurt'})<-[:HABITA_EN]-(a:Bestia) 
RETURN count(*) AS Número 
----
*Objetivo*: Número de bestias que habitan en la ciudad Oxenfurt 
*Resultado*: 
image:images/q2.png[q2, title="q2"]

*Consulta*: 
//output
[source,cypher]
----
MATCH (f:Faccion)<-[:PERTENECE]-(a:Personaje) 
RETURN count(*) AS NUMERO_PERSONAJES, f.nombre_faccion AS FACCION 
----
*Objetivo*: Saber el número de personajes que pertenecen a cada una de las facciones 
*Resultado*: 
image:images/q3.png[q3, title="q3"]

===Consultas intermedias
*Consulta*:
//output
[source,cypher]
----
MATCH (c:Ciudad)<-[:BORN]-(p:Personaje) 
WHERE NOT (:Pais)<-[:CAPITAL_DE]-(c) 
RETURN DISTINCT p.nombre_personaje AS Nombre 
----
*Objetivo*: Obtener los personajes que no hayan nacido en la capital de un país 
*Resultado*: 
image:images/q4.png[q4, title="q4"]

*Consulta*:
//output
[source,cypher]
----
MATCH (c:Ciudad)<-[:BORN]-(p:Personaje) 
WHERE (:Bestia {nombre_bestia:'Moira'})-[:HABITA_EN]->(c) 
RETURN DISTINCT p.nombre_personaje AS Nombre 
----
*Objetivo*: Obtener los personajes que hayan nacido en una ciudad que sea el hábitat de la Moira 
*Resultado*: 
image:images/q5.png[q5, title="q5"]

*Consulta*:
//output
[source,cypher]
----
MATCH(c:Ciudad{name_ciudad:'Novigrado'})<-[:BORN]-(p:Personaje)-[:PERTENECE]->(f:Faccion) 
WITH f, count(p) AS numero 
ORDER BY numero DESC 
RETURN f.nombre_faccion, numero 
----
*Objetivo*: Número de personajes que pertenecen a una facción que hayan nacido en Novigrado 
*Resultado*: 
image:images/q6.png[q6, title="q6"]

*Consulta*: 
//output
[source,cypher]
----
MATCH(p:Personaje)-[:BORN]->(c:Ciudad)-[:CAPITAL_DE]->(pa:Pais)-[:FORMA]->(f:Faccion) 
WHERE NOT (p)-[:PERTENECE]-(f)  
RETURN p.nombre_personaje AS nombre 
----
*Objetivo*: Personajes que hayan nacido en una ciudad que sea capital de un país que no forma la facción a la que el personaje pertenece 
*Resultado*: 
image:images/q7.png[q7, title="q7"]

===Consultas complejas
*Consulta*: 
//output
[source,cypher]
----
//
----
*Objetivo*: Facciones defensoras en una guerra que sucede en un país, que es el mismo que forma a una facción atacante 
*Resultado*: 
image:images/q8.png[q8, title="q8"]
image:images/q8_g.png[q8, title="q8"]

*Consulta*: 
//output
[source,cypher]
----
MATCH(p:Personaje)-[:GOBIERNA]->(pais:Pais)-[:FORMA]->(f:Faccion)-[parte:TOMA_PARTE]->(a:Armisticio) 
WHERE parte.postura='Atacante' 
RETURN DISTINCT p.nombre_personaje as Gobernante_atacante, a.nombre_armisticio as Guerra 
----
*Objetivo*:  Personajes que gobiernan un país que forma una facción de las atacantes en un armisticio 
*Resultado*: 
image:images/q9.png[q9, title="q9"]


==APLICACIÓN GRÁFICA
La aplicación gráfica está programada en Java, y cuenta con una lista de opciones para ejecutar las consultas citadas anteriormente, así como modificarlas, o escribir nuevas consultas. Una vez ejecutadas, el resultado se visualizará en la propia aplicación. 


